buildscript {
  dependencies {
    classpath group: 'biz.aQute.bnd', name: 'biz.aQute.bnd.gradle', version: '5.0.1'
  }
}

plugins {
  id "java"
  id "maven-publish"
  id "signing"
}

apply plugin: 'biz.aQute.bnd.builder'

repositories {
  mavenLocal()
  mavenCentral()
}

configurations {
  fatjar.extendsFrom(implementation)
}

configurations.all {
  resolutionStrategy {
    force 'xml-apis:xml-apis:1.4.01'
  }
}

dependencies {
  implementation (
    [group: 'net.sf.saxon', name: 'Saxon-HE', version: "9.9.1-7"],
    [group: 'nu.validator.htmlparser', name: 'htmlparser', version: '1.4']
  )
}

compileJava {
    options.compilerArgs += ['-Xlint:deprecation']
}

jar {
  archiveBaseName = "html5parser"
  bnd ('Built-By': 'Norman Walsh',
       'Implementation-Vendor': 'Norman Walsh',
       'Implementation-Title': 'Html5Parser',
       'Implementation-Version': "1.0.0",
       'Main-Class': 'com.nwalsh.parsers.Html5Parser')
}

task fatJar(type: Jar) {
  manifest {
    attributes 'Main-Class': 'com.nwalsh.parsers.Html5Parser'
  }
  archiveBaseName = 'html5parser'
  from {
    configurations.fatjar.collect { it.isDirectory() ? it : zipTree(it) }
  }
  with jar
}

//task bundle(type: Bundle) {
//  from sourceSets.main.output
//}

task dist(dependsOn: ["test", "jar", "fatJar"], type: Copy) {
  from "build/libs"
  into "build/dist"
  doFirst {
    mkdir "build/dist"
  }
}
